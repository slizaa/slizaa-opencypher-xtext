grammar org.slizaa.neo4j.opencypher.OpenCypher hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate openCypher "http://www.slizaa.org/neo4j/opencypher/OpenCypher"

Cypher:
/*
 * oC_Cypher : SP? oC_QueryOptions oC_Statement ( SP? ';' )? SP? EOF ;
 */
	{Cypher} queryOptions=QueryOptions statement=Statement ';'?;

QueryOptions:
/*
 * oC_QueryOptions : ( oC_AnyCypherOption SP? )* ;
 * oC_AnyCypherOption : oC_CypherOption
 *                    | oC_Explain
 *                    | oC_Profile
 *                    ;
 * oC_Explain : EXPLAIN ;
 * oC_Profile : PROFILE ;
 */
	{AllOptions} (explain?='EXPLAIN'? & profile?='PROFILE'? & cypherOption+=CypherOption*);

CypherOption:
/*
 * oC_CypherOption : CYPHER ( SP oC_VersionNumber )? ( SP oC_ConfigurationOption )* ;
 */
	{CypherOption} 'CYPHER' (versionNumber=VersionNumber)? (configurationOption+=ConfigurationOption)*;

VersionNumber:
/*
 * oC_VersionNumber : RegularDecimalReal ;
 */
	versionNumber=Number;

ConfigurationOption:
/*
 * oC_ConfigurationOption : oC_SymbolicName SP? '=' SP? oC_SymbolicName ;
 */
	key=SYMBOLIC_NAME_X '=' value=SYMBOLIC_NAME_X;

Statement:
/*
 * oC_Statement : oC_Command
 *              | oC_Query
 *              ;
 */
	Command | Query;

Query:
/*
 * oC_Query : oC_RegularQuery
 *          | oC_StandaloneCall
 *          | oC_BulkImportQuery
 *          ;
 */
	RegularQuery | StandaloneCall | BulkImportQuery;

RegularQuery:
/*
 * oC_RegularQuery : oC_SingleQuery ( SP? oC_Union )* ;
 */
	SingleQuery ({CombinedQuery.singleQuery=current} (union+=Union)+)?;

BulkImportQuery:
/*
 * oC_BulkImportQuery : oC_PeriodicCommitHint SP? oC_LoadCSVQuery ;
 */
	periodicCommitHint=PeriodicCommitHint loadCSVQuery=LoadCSVQuery;

PeriodicCommitHint:
/*
 * oC_PeriodicCommitHint : USING SP PERIODIC SP COMMIT ( SP oC_IntegerLiteral )? ;
 */
	{PeriodicCommitHint} 'USING' 'PERIODIC' 'COMMIT' numberOfRowsPerCommit=DecimalInteger?;

LoadCSVQuery:
/*
 * oC_LoadCSVQuery : oC_LoadCSV oC_SingleQuery ;
 */
	loadCSV=LoadCSV singleQuery=SingleQuery;

Union:
/*
 * oC_Union : ( UNION SP ALL SP? oC_SingleQuery )
 *          | ( UNION SP? oC_SingleQuery )
 *          ;
 */
	('UNION' all?='ALL'? singleQuery=SingleQuery);

SingleQuery:
/*
 * oC_SingleQuery : oC_SinglePartQuery
 *                | oC_MultiPartQuery
 *                ;
 * 
 * Comment: For some obscure reason the order is important here!
 */
	MultiPartQuery | SinglePartQuery;

SinglePartQuery:
/*
 * oC_SinglePartQuery : ( ( oC_ReadingClause SP? )* oC_Return )
 *                    | ( ( oC_ReadingClause SP? )* oC_UpdatingClause ( SP? oC_UpdatingClause )* ( SP? oC_Return )? )
 *                    ;
 */
	(readingClauses+=ReadingClause)* return=Return | 
	(readingClauses+=ReadingClause)* (updatingClauses+=UpdatingClause)+ (return=Return)? ;

MultiPartQuery:
/*
 * oC_MultiPartQuery : ( ( oC_ReadingClause SP? )* ( oC_UpdatingClause SP? )* oC_With SP? )+ oC_SinglePartQuery ;
 * 
 * Comment: used the name 'withPart' instead of with as 'with' is a key word of xtext
 * Comment: introduced MultiPartSubQuery to encapsulate the repeating group '( oC_ReadingClause SP? )* ( oC_UpdatingClause SP? )* oC_With SP?'
 */
	(subQueries+=MultiPartSubQuery)+ singlePartQuery=SinglePartQuery;

MultiPartSubQuery:
/*
 * ( oC_ReadingClause SP? )* ( oC_UpdatingClause SP? )* oC_With SP?
 */
	(readingClause+=ReadingClause)* (updatingClauses+=UpdatingClause)* withPart=With;

Clause:
/*
 * Comment: introduced Clause to have a common superinterface for the different clause types. This comes handy when dealing with the ASG.
 */
	UpdatingClause | ReadingClause | Return | With;

UpdatingClause:
/*
 * oC_UpdatingClause : oC_Create
 *                   | oC_Merge
 *                   | oC_CreateUnique
 *                   | oC_Foreach
 *                   | oC_Delete
 *                   | oC_Set
 *                   | oC_Remove
 *                   ;
 */
	Create | Merge | CreateUnique | Foreach | Delete | Set | Remove;

ReadingClause:
/*
 * oC_ReadingClause : oC_LoadCSV
 *                  | oC_Start
 *                  | oC_Match
 *                  | oC_Unwind
 *                  | oC_InQueryCall
 *                  ;
 */
	LoadCSV | Start | Match | Unwind | InQueryCall;

Command:
/*
 * oC_Command : oC_CreateIndex
 *            | oC_DropIndex
 *            | oC_CreateUniqueConstraint
 *            | oC_DropUniqueConstraint
 *            | oC_CreateNodePropertyExistenceConstraint
 *            | oC_DropNodePropertyExistenceConstraint
 *            | oC_CreateRelationshipPropertyExistenceConstraint
 *            | oC_DropRelationshipPropertyExistenceConstraint
 *            ;
 */
	CreateIndex | DropIndex | CreateUniqueConstraint | DropUniqueConstraint | CreateNodePropertyExistenceConstraint |
	DropNodePropertyExistenceConstraint | CreateRelationshipPropertyExistenceConstraint |
	DropRelationshipPropertyExistenceConstraint;

CreateUniqueConstraint:
/*
 * oC_CreateUniqueConstraint : CREATE SP oC_UniqueConstraint ;
 */
	'CREATE' UniqueConstraint;

CreateNodePropertyExistenceConstraint:
/*
 * oC_CreateNodePropertyExistenceConstraint : CREATE SP oC_NodePropertyExistenceConstraint ;
 */
	'CREATE' NodePropertyExistenceConstraint;

CreateRelationshipPropertyExistenceConstraint:
/*
 * oC_CreateRelationshipPropertyExistenceConstraint : CREATE SP oC_RelationshipPropertyExistenceConstraint ;
 */
	'CREATE' RelationshipPropertyExistenceConstraint;

CreateIndex:
/*
 * oC_CreateIndex : CREATE SP oC_Index ;
 */
	'CREATE' index=Index;

DropUniqueConstraint:
/*
 * oC_DropUniqueConstraint : DROP SP oC_UniqueConstraint ;
 */
	'DROP' uniqueConstraint=UniqueConstraint;

DropNodePropertyExistenceConstraint:
/*
 * oC_DropNodePropertyExistenceConstraint : DROP SP oC_NodePropertyExistenceConstraint ;
 */
	'DROP' nodePropertyExistenceConstraint=NodePropertyExistenceConstraint;

DropRelationshipPropertyExistenceConstraint:
/*
 * oC_DropRelationshipPropertyExistenceConstraint : DROP SP oC_RelationshipPropertyExistenceConstraint ;
 */
	'DROP' relationshipPropertyExistenceConstraint=RelationshipPropertyExistenceConstraint;

DropIndex:
/*
 * oC_DropIndex : DROP SP oC_Index ;
 */
	'DROP' index=Index;

Index:
/*
 * oC_Index : INDEX SP ON SP? oC_NodeLabel '(' oC_PropertyKeyName ')' ;
 */
	'INDEX' 'ON' nodeLabel=NodeLabel '(' propertyKeyName=PropertyKeyName ')';

UniqueConstraint:
/*
 * oC_UniqueConstraint : CONSTRAINT SP ON SP? '(' oC_Variable oC_NodeLabel ')' SP? ASSERT SP oC_PropertyExpression SP IS SP UNIQUE ;
 */
	'CONSTRAINT' 'ON' '(' variable=VariableDeclaration nodeLabel=NodeLabel ')' 'ASSERT'
	propertyExpression=PropertyExpression 'IS' 'UNIQUE';

NodePropertyExistenceConstraint:
/*
 * oC_NodePropertyExistenceConstraint : CONSTRAINT SP ON SP? '(' oC_Variable oC_NodeLabel ')' SP? ASSERT SP EXISTS SP? '(' oC_PropertyExpression ')' ;
 */
	'CONSTRAINT' 'ON' '(' variable=VariableDeclaration nodeLabel=NodeLabel ')' 'ASSERT' 'EXISTS' '('
	propertyExpression=PropertyExpression ')';

RelationshipPropertyExistenceConstraint:
/*
 * oC_RelationshipPropertyExistenceConstraint : CONSTRAINT SP ON SP? oC_RelationshipPatternSyntax SP? ASSERT SP EXISTS SP? '(' oC_PropertyExpression ')' ;
 */
	'CONSTRAINT' 'ON' relationshipPattern=RelationshipPatternSyntax 'ASSERT' 'EXISTS' '('
	propertyExpression=PropertyExpression ')';

RelationshipPatternSyntax:
/*
 * oC_RelationshipPatternSyntax : ( '(' SP? ')' oC_Dash '[' oC_Variable oC_RelType ']' oC_Dash '(' SP? ')' )
 *                              | ( '(' SP? ')' oC_Dash '[' oC_Variable oC_RelType ']' oC_Dash oC_RightArrowHead '(' SP? ')' )
 *                              | ( '(' SP? ')' oC_LeftArrowHead oC_Dash '[' oC_Variable oC_RelType ']' oC_Dash '(' SP? ')' )
 *                              ;
 */
	('(' ')' incoming?=LeftArrowHead? Dash '[' variable=VariableDeclaration relType=RelType ']' Dash
	outgoing?=RightArrowHead? '(' ')');

LoadCSV:
/*
 * oC_LoadCSV : LOAD SP CSV SP ( WITH SP HEADERS SP )? FROM SP oC_Expression SP AS SP oC_Variable SP ( FIELDTERMINATOR SP StringLiteral )? ;
 * 
 * Comment: For simplicity we split up the antlr clause in xtext clauses 'LoadCSV' and 'LoadCSVWithHeaders'
 */
	{LoadCSV} 'LOAD' 'CSV' withHeaders?=LoadCSVWithHeaders? 'FROM' expression=Expression 'AS'
	variable=VariableDeclaration ('FIELDTERMINATOR' fieldterminator=STRING_LITERAL)?;

LoadCSVWithHeaders:
	'WITH' 'HEADERS';

Match:
/*
 * oC_Match : ( OPTIONAL SP )? MATCH SP? oC_Pattern ( oC_Hint )* ( SP? oC_Where )? ;
 */
	optional?='OPTIONAL'? 'MATCH' pattern=Pattern (hints+=Hint)* where=Where?;

Unwind:
/*
 * oC_Unwind : UNWIND SP? oC_Expression SP AS SP oC_Variable ;
 */
	'UNWIND' expression=Expression 'AS' variable=VariableDeclaration;

Merge:
/*
 * oC_Merge : MERGE SP? oC_PatternPart ( SP oC_MergeAction )* ;
 */
	'MERGE' patternPart=PatternPart (mergeActions+=MergeAction)*;

MergeAction:
/*
 * oC_MergeAction : ( ON SP MATCH SP oC_Set )
 *                | ( ON SP CREATE SP oC_Set )
 *                ;
 */
	'ON' action=('MATCH' | 'CREATE') set=Set;

Create:
/*
 * oC_Create : CREATE SP? oC_Pattern ;
 */
	'CREATE' pattern=Pattern;

CreateUnique:
/*
 * oC_CreateUnique : CREATE SP UNIQUE SP? oC_Pattern ;
 */
	'CREATE' uniqueContraint=UniqueConstraint;

Set:
/*
 * oC_Set : SET SP? oC_SetItem ( ',' oC_SetItem )* ;
 */
	'SET' setItems+=SetItem (',' setItems+=SetItem)*;

SetItem:
/*
 * oC_SetItem : ( oC_PropertyExpression SP? '=' SP? oC_Expression )
 *            | ( oC_Variable SP? '=' SP? oC_Expression )
 *            | ( oC_Variable SP? '+=' SP? oC_Expression )
 *            | ( oC_Variable SP? oC_NodeLabels )
 *            ;
 */
	(propertyExpression=PropertyExpression '=' expression=Expression) | (variable=VariableDeclaration '='
	expression=Expression) | (variable=VariableDeclaration '+=' expression=Expression) | (variable=VariableDeclaration
	NodeLabels);

Delete:
/*
 * oC_Delete : ( DETACH SP )? DELETE SP? oC_Expression ( SP? ',' SP? oC_Expression )* ;
 */
	(detach?='DETACH')? 'DELETE' expressions+=Expression (',' expressions+=Expression)*;

Remove:
/*
 * oC_Remove : REMOVE SP oC_RemoveItem ( SP? ',' SP? oC_RemoveItem )* ;
 */
	'REMOVE' removeItems+=RemoveItem (',' removeItems+=RemoveItem)*;

RemoveItem:
/*
 * oC_RemoveItem : ( oC_Variable oC_NodeLabels )
 *               | oC_PropertyExpression
 *               ;
 */
	({RemoveItemLabel} variable=VariableDeclaration NodeLabels) | {RemoveItemProperty} propertyExpression=PropertyExpression;

Foreach:
/*
 * oC_Foreach : FOREACH SP? '(' SP? oC_Variable SP IN SP oC_Expression SP? '|' ( SP oC_UpdatingClause )+ SP? ')' ;
 */
	'FOREACH' '(' variable=VariableDeclaration 'IN' expression=Expression '|' (updatingClauses+=UpdatingClause)+ ')';

InQueryCall:
/*
 * oC_InQueryCall : CALL SP oC_ExplicitProcedureInvocation ( SP? YIELD SP oC_YieldItems )? ;
 */
	'CALL' invocation=ExplicitProcedureInvocation ('YIELD' yieldItems=YieldItems)?;

StandaloneCall:
/*
 * oC_StandaloneCall : CALL SP ( oC_ExplicitProcedureInvocation | oC_ImplicitProcedureInvocation ) ( SP YIELD SP oC_YieldItems )? ;
 */
	'CALL' invocation=(ExplicitProcedureInvocation | ImplicitProcedureInvocation) ('YIELD' yieldItems=YieldItems)?;

YieldItems:
/*
 * oC_YieldItems : ( oC_YieldItem ( SP? ',' SP? oC_YieldItem )* )
 *               | '-'
 *               ;
 */
	{YieldItems} ((items+=YieldItem (',' items+=YieldItem)*) | '-');

YieldItem:
/*
 * oC_YieldItem : ( oC_ProcedureResultField SP AS SP )? oC_Variable ;
 */
	(field=ProcedureResultField 'AS')? variable=VariableDeclaration;

With:
/*
 * oC_With : WITH ( SP? DISTINCT )? SP oC_ReturnBody ( SP? oC_Where )? ;
 */
	'WITH' distint?='DISTINCT'? returnBody=ReturnBody where=Where?;

Return:
/*
 * oC_Return : RETURN ( SP? DISTINCT )? SP oC_ReturnBody ;
 */
	(return='RETURN' distinct?='DISTINCT'? body=ReturnBody);

ReturnBody:
/*
 * oC_ReturnBody : oC_ReturnItems ( SP oC_Order )? ( SP oC_Skip )? ( SP oC_Limit )? ;
 */
	returnItems=ReturnItems (order=Order)? (skip=Skip)? (limit=Limit)?;

ReturnItems:
/*
 * oC_ReturnItems : ( '*' ( SP? ',' SP? oC_ReturnItem )* )
 *                | ( oC_ReturnItem ( SP? ',' SP? oC_ReturnItem )* )
 *                ;
 */
	{ReturnItems} (all='*' (',' items+=ReturnItem)*) | (items+=ReturnItem (',' items+=ReturnItem)*);

ReturnItem:
/*
 * oC_ReturnItem : ( oC_Expression SP AS SP oC_Variable )
 *               | oC_Expression
 *               ;
 */
	expression=Expression ('AS' alias=VariableDeclaration)?;

Order:
/*
 * oC_Order : ORDER SP BY SP oC_SortItem ( ',' SP? oC_SortItem )* ;
 */
	'ORDER' 'BY' orderBy+=SortItem (',' orderBy+=SortItem)*;

Skip:
/*
 * oC_Skip : L_SKIP SP oC_Expression ;
 */
	'SKIP' skip=Expression;

Limit:
/*
 * oC_Limit : LIMIT SP oC_Expression ;
 */
	'LIMIT' limit=Expression;

SortItem:
/*
 * oC_SortItem : oC_Expression ( SP? ( ASCENDING | ASC | DESCENDING | DESC ) )? ;
 */
	(expression=Expression sort=('DESCENDING' | 'DESC' | 'ASCENDING' | 'ASC')?);

Hint hidden():
/*
 * oC_Hint : SP? ( ( USING SP INDEX SP oC_Variable oC_NodeLabel '(' oC_PropertyKeyName ')' ) | ( USING SP JOIN SP ON SP oC_Variable ( SP? ',' SP? oC_Variable )* ) | ( USING SP SCAN SP oC_Variable oC_NodeLabel ) ) ;
 */
	{IndexHint} 'USING' WS 'INDEX' WS variable=VariableDeclaration nodeLabel=NodeLabel WS '('
	propertyKeyName=PropertyKeyName ')' | {JoinHint} ('USING' WS 'JOIN' WS 'ON' variables+=VariableDeclaration (','
	variables+=VariableDeclaration)*) | {ScanHint} ('USING' WS 'SCAN' WS variable=VariableDeclaration
	nodeLabel=NodeLabel);

Start:
/*
 * oC_Start : START SP oC_StartPoint ( SP? ',' SP? oC_StartPoint )* oC_Where? ;
 */
	'START' startPoint+=StartPoint (',' startPoint+=StartPoint)* where=Where?;

StartPoint:
/*
 * oC_StartPoint : oC_Variable SP? '=' SP? oC_Lookup ;
 */
	variable=VariableDeclaration '=' lookup=Lookup;

Lookup:
/*
 * oC_Lookup : oC_NodeLookup
 *           | oC_RelationshipLookup
 *           ;
 */
	NodeLookup | RelationshipLookup;

NodeLookup:
/*
 * oC_NodeLookup : NODE SP? ( oC_IdentifiedIndexLookup | oC_IndexQuery | oC_IdLookup ) ;
 */
	'NODE' (IdentifiedIndexLookup | IndexQuery | IdLookup);

RelationshipLookup:
/*
 * oC_RelationshipLookup : ( RELATIONSHIP | REL ) ( oC_IdentifiedIndexLookup | oC_IndexQuery | oC_IdLookup ) ;
 */
	('RELATIONSHIP' | 'REL') (IdentifiedIndexLookup | IndexQuery | IdLookup);

IdentifiedIndexLookup:
/*
 * oC_IdentifiedIndexLookup : ':' oC_SymbolicName '(' oC_SymbolicName '=' ( StringLiteral | oC_LegacyParameter ) ')' ;
 */
	':' indexName=SYMBOLIC_NAME_X '(' key=SYMBOLIC_NAME_X '=' (value=STRING_LITERAL | legacyParameter=LegacyParameter)
	')';

IndexQuery:
/*
 * oC_IndexQuery : ':' oC_SymbolicName '(' ( StringLiteral | oC_LegacyParameter ) ')' ;
 */
	':' indexName=SYMBOLIC_NAME_X '(' (query=STRING_LITERAL | parameter=Parameter) ')';

IdLookup:
/*
 * oC_IdLookup : '(' ( oC_LiteralIds | oC_LegacyParameter | '*' ) ')' ;
 */
	'(' (ids=LiteralIds | legacyParameter=Parameter | wildcard='*') ')';

LiteralIds:
/*
 * oC_LiteralIds : oC_IntegerLiteral ( SP? ',' SP? oC_IntegerLiteral )* ;
 */
	ids+=DecimalInteger (',' ids+=DecimalInteger)*;

Where:
/*
 * oC_Where : WHERE SP oC_Expression ;
 */
	'WHERE' expression=Expression;

Pattern:
/*
 * oC_Pattern : oC_PatternPart ( SP? ',' SP? oC_PatternPart )* ;
 */
	patterns+=PatternPart (',' patterns+=PatternPart)*;

PatternPart:
/*
 * oC_PatternPart : ( oC_Variable SP? '=' SP? oC_AnonymousPatternPart )
 *                | oC_AnonymousPatternPart
 *                ;
 */
	(var=VariableDeclaration '=' part=AnonymousPatternPart) | AnonymousPatternPart;

AnonymousPatternPart:
/*
 * oC_AnonymousPatternPart : oC_ShortestPathPattern
 *                         | oC_PatternElement
 *                         ;
 */
	ShortestPathPattern | PatternElement;

ShortestPathPattern:
/*
 * oC_ShortestPathPattern : ( SHORTESTPATH '(' oC_PatternElement ')' )
 *                        | ( ALLSHORTESTPATHS '(' oC_PatternElement ')' )
 *                        ;
 */
	{ShortestPath} 'SHORTESTPATH' '(' patternElement=PatternElement ')' | {AllShortestPaths} 'ALLSHORTESTPATHS' '('
	patternElement=PatternElement ')';

PatternElement:
/*
 * oC_PatternElement : ( oC_NodePattern ( SP? oC_PatternElementChain )* )
 *                   | ( '(' oC_PatternElement ')' )
 *                   ;
 */
	(nodepattern=NodePattern (chain+=PatternElementChain)*) | '(' PatternElement ')';

NodePattern:
/*
 * oC_NodePattern : '(' SP? ( oC_Variable SP? )? ( oC_NodeLabels SP? )? ( oC_Properties SP? )? ')' ;
 */
	{NodePattern} '(' variable=VariableDeclaration? NodeLabels? properties=Properties? ')';

PatternElementChain:
/*
 * oC_PatternElementChain : oC_RelationshipPattern SP? oC_NodePattern ;
 */
	relationshipPattern=RelationshipPattern nodePattern=NodePattern;

RelationshipPattern:
/*
 * oC_RelationshipPattern : ( oC_LeftArrowHead SP? oC_Dash SP? oC_RelationshipDetail? SP? oC_Dash SP? oC_RightArrowHead )
 *                        | ( oC_LeftArrowHead SP? oC_Dash SP? oC_RelationshipDetail? SP? oC_Dash )
 *                        | ( oC_Dash SP? oC_RelationshipDetail? SP? oC_Dash SP? oC_RightArrowHead )
 *                        | ( oC_Dash SP? oC_RelationshipDetail? SP? oC_Dash )
 *                        ;
 */
	{RelationshipPattern} (incoming?=LeftArrowHead? Dash detail=RelationshipDetail? Dash outgoing?=RightArrowHead?);

RelationshipDetail:
/*
 * oC_RelationshipDetail : '[' SP? ( oC_Variable SP? )? ( oC_RelationshipTypes SP? )? oC_RangeLiteral? ( oC_Properties SP? )? ']' ;
 */
	{RelationshipDetail} '[' variable=VariableDeclaration? optional?='?'? RelationshipTypes? (range=RangeLiteral)?
	properties=Properties? ']';

Properties:
/*
 * oC_Properties : oC_MapLiteral
 *               | oC_Parameter
 *               | oC_LegacyParameter
 *               ;
 */
	MapLiteral | Parameter | LegacyParameter;

RelType:
/*
 * oC_RelType : ':' SP? oC_RelTypeName ;
 */
	':' RelTypeName;

fragment RelationshipTypes:
/*
 * oC_RelationshipTypes : ':' SP? oC_RelTypeName ( SP? '|' ':'? SP? oC_RelTypeName )* ;
 */
	':' relTypeNames+=RelTypeName ('|' ':'? relTypeNames+=RelTypeName)*;

fragment NodeLabels:
/*
 * oC_NodeLabels : oC_NodeLabel ( SP? oC_NodeLabel )* ;
 */
	nodeLabels+=NodeLabel+;

NodeLabel:
/*
 * oC_NodeLabel : ':' SP? oC_LabelName ;
 */
	':' labelName=LabelName;

RangeLiteral:
/*
 * oC_RangeLiteral : '*' SP? ( oC_IntegerLiteral SP? )? ( '..' SP? ( oC_IntegerLiteral SP? )? )? ;
 * 
 * Comment: Format of IntegerLiteral is checked via 
 * org.slizaa.neo4j.opencypher.validation.OpenCypherValidator.checkRangeLiteralFormat(RangeLiteral)
 */
	{RangeLiteral} '*' (lower=Number? (variableLength?='..' upper=Number?)?)?;

LabelName:
/*
 * oC_LabelName : oC_SchemaName ;
 */
// TODO
	SYMBOLIC_NAME_X;

RelTypeName:
/*
 * oC_RelTypeName : oC_SchemaName ;
 */
// TODO
	SYMBOLIC_NAME_X;

Expression:
/*
 * oC_Expression : oC_OrExpression ;
 */
	OrExpression;

OrExpression returns Expression:
/*
 * oC_OrExpression : oC_XorExpression ( SP OR SP oC_XorExpression )* ;
 */
	XorExpression ({OrExpression.left=current} operator='OR' right=XorExpression)*;

XorExpression returns Expression:
/*
 * oC_XorExpression : oC_AndExpression ( SP XOR SP oC_AndExpression )* ;
 */
	AndExpression ({XorExpression.left=current} operator='XOR' right=AndExpression)*;

AndExpression returns Expression:
/*
 * oC_AndExpression : oC_NotExpression ( SP AND SP oC_NotExpression )* ;
 */
	NotExpression ({AndExpression.left=current} operator='AND' right=NotExpression)*;

NotExpression returns Expression:
/*
 * oC_NotExpression : ( NOT SP? )* oC_ComparisonExpression ;
 * 
 * Comment: Clause is modeled on the former antlr clause 'expression9 : ( sp NOT sp expression9 ) | expression8;'
 * We should eventually adjust this...
 */
//TODO
	({NotExpression} operator='NOT' left=NotExpression) | ComparisonExpression;

ComparisonExpression returns Expression:
/*
 * oC_ComparisonExpression : oC_AddOrSubtractExpression ( SP? oC_PartialComparisonExpression )* ;
 * 
 * oC_PartialComparisonExpression : ( '=' SP? oC_AddOrSubtractExpression )
 *                                | ( '<>' SP? oC_AddOrSubtractExpression )
 *                                | ( '<' SP? oC_AddOrSubtractExpression )
 *                                | ( '>' SP? oC_AddOrSubtractExpression )
 *                                | ( '<=' SP? oC_AddOrSubtractExpression )
 *                                | ( '>=' SP? oC_AddOrSubtractExpression )
 *                                ;
 * 
 * Comment: combined clause for 'oC_ComparisonExpression' and 'oC_PartialComparisonExpression'
 */
	AddOrSubtractExpression ({ComparisonExpression.left=current} operator=('=' | '<>' | '<' | '>' | '<=' | '>=')
	right=AddOrSubtractExpression)*;

AddOrSubtractExpression returns Expression:
/*
 * oC_AddOrSubtractExpression : oC_MultiplyDivideModuloExpression ( ( SP? '+' SP? oC_MultiplyDivideModuloExpression ) | ( SP? '-' SP? oC_MultiplyDivideModuloExpression ) )* ;
 */
	MultiplyDivideModuloExpression ({AddOrSubtractExpression.left=current} operator=('+' | '-')
	right=MultiplyDivideModuloExpression)*;

MultiplyDivideModuloExpression returns Expression:
/*
 * oC_MultiplyDivideModuloExpression : oC_PowerOfExpression ( ( SP? '*' SP? oC_PowerOfExpression ) | ( SP? '/' SP? oC_PowerOfExpression ) | ( SP? '%' SP? oC_PowerOfExpression ) )* ;
 */
	PowerOfExpression ({MultiplyDivideModuloExpression.left=current} operator=('*' | '/' | '%') right=PowerOfExpression)*;

PowerOfExpression returns Expression:
/*
 * oC_PowerOfExpression : oC_UnaryAddOrSubtractExpression ( SP? '^' SP? oC_UnaryAddOrSubtractExpression )* ;
 */
	UnaryAddOrSubtractExpression ({PowerOfExpression.left=current} operator='^' right=UnaryAddOrSubtractExpression)*;

UnaryAddOrSubtractExpression returns Expression:
/*
 * oC_UnaryAddOrSubtractExpression : ( ( '+' | '-' ) SP? )* oC_StringListNullOperatorExpression ;
 */
	StringListNullOperatorExpression | {UnaryAddOrSubtractExpression} operator=('+' | '-') left=UnaryAddOrSubtractExpression;

StringListNullOperatorExpression returns Expression:
/*
 * oC_StringListNullOperatorExpression : oC_PropertyOrLabelsExpression ( ( SP? '[' oC_Expression ']' ) | ( SP? '[' oC_Expression? '..' oC_Expression? ']' ) | ( ( oC_RegularExpression | ( SP IN ) | ( SP STARTS SP WITH ) | ( SP ENDS SP WITH ) | ( SP CONTAINS ) ) SP? oC_PropertyOrLabelsExpression ) | ( SP IS SP NULL ) | ( SP IS SP NOT SP NULL ) )* ;
 */
	PropertyOrLabelsExpression (({IndexLookupExpression.left=current} '[' expression=Expression ']') |
	({IndexRangeExpression.left=current} '[' (lower=Expression)? '..' (upper=Expression)? ']') |
	({RegExpMatchingExpression.left=current} '=~' right=PropertyOrLabelsExpression) |
	({InCollectionExpression.left=current} 'IN' right=PropertyOrLabelsExpression) | ({StartsWithExpression.left=current}
	'STARTS' 'WITH' right=PropertyOrLabelsExpression) | ({EndsWithExpression.left=current} 'ENDS' 'WITH'
	right=PropertyOrLabelsExpression) | ({ContainsExpression.left=current} 'CONTAINS' right=PropertyOrLabelsExpression) |
	({IsNullExpression.left=current} 'IS' 'NULL') | ({IsNotNullExpression.left=current} 'IS' 'NOT' 'NULL'))*;

PropertyOrLabelsExpression returns Expression:
/*
 * oC_PropertyOrLabelsExpression : oC_Atom ( SP? oC_PropertyLookup )* ( SP? oC_NodeLabels )? ;
 *
 * Note: we want PropertyLookupExpression and NodeLabelsExpression to
 * appear in the AST iff property lookups and node label constraints were given, respectively
 */
	Atom ({PropertyLookupExpression.left=current} propertyLookups+=PropertyLookup+)?
	     ({NodeLabelsExpression.left=current} NodeLabels)?
	;

Atom returns Expression:
/*
 * oC_Atom : oC_Literal
 *         | oC_Parameter
 *         | oC_LegacyParameter
 *         | oC_CaseExpression
 *         | ( COUNT SP? '(' SP? '*' SP? ')' )
 *         | oC_ListComprehension
 *         | oC_PatternComprehension
 *         | ( FILTER SP? '(' SP? oC_FilterExpression SP? ')' )
 *         | ( EXTRACT SP? '(' SP? oC_FilterExpression SP? ( SP? '|' oC_Expression )? ')' )
 *         | oC_Reduce
 *         | ( ALL SP? '(' SP? oC_FilterExpression SP? ')' )
 *         | ( ANY SP? '(' SP? oC_FilterExpression SP? ')' )
 *         | ( NONE SP? '(' SP? oC_FilterExpression SP? ')' )
 *         | ( SINGLE SP? '(' SP? oC_FilterExpression SP? ')' )
 *         | oC_ShortestPathPattern
 *         | oC_RelationshipsPattern
 *         | oC_ParenthesizedExpression
 *         | oC_FunctionInvocation
 *         | oC_Variable
 *         ;
 */
	Literal | Parameter | LegacyParameter | CaseExpression | {Count} 'COUNT' '(' '*' ')' | ListComprehension |
	PatternComprehension | {Filter} 'FILTER' '(' filterExpression=FilterExpression ')' | {Extract} 'EXTRACT' '('
	filterExpression=FilterExpression ('|' expression=Expression)? ')' | Reduce | {All} 'ALL' '('
	filterExpression=FilterExpression ')' | {Any} 'ANY' '(' filterExpression=FilterExpression ')' | {None} 'NONE' '('
	filterExpression=FilterExpression ')' | {Single} 'SINGLE' '(' filterExpression=FilterExpression ')' |
	ShortestPathPattern | RelationshipsPattern | ParenthesizedExpression | FunctionInvocation |
	// TODO 
	{VariableRef} variableRef=[VariableDeclaration|SYMBOLIC_NAME_X];

Literal:
/*
 * oC_Literal : oC_NumberLiteral
 *            | StringLiteral
 *            | oC_BooleanLiteral
 *            | NULL
 *            | oC_MapLiteral
 *            | oC_ListLiteral
 *            ;
 */
	NumberLiteral | StringLiteral | {NULL} 'NULL' | BooleanLiteral | MapLiteral | ListLiteral;

BooleanLiteral:
/*
 * oC_BooleanLiteral : TRUE
 *                   | FALSE
 *                   ;
 */
	{BooleanLiteral} value=('TRUE' | 'FALSE');

ListLiteral:
/*
 * oC_ListLiteral : '[' SP? ( oC_Expression SP? ( ',' SP? oC_Expression SP? )* )? ']' ;
 */
	{ListLiteral} '[' (expressions+=Expression (',' expressions+=Expression)*)? ']';

Reduce:
/*
 * REDUCE SP? '(' oC_Variable '=' oC_Expression ',' oC_IdInColl '|' oC_Expression ')' ;
 */
	'REDUCE' '(' accumulator=VariableDeclaration '=' accumulatorExpression=Expression ',' idInColl=IdInColl '|'
	expression=Expression ')';

ParenthesizedExpression:
/*
 * oC_ParenthesizedExpression : '(' SP? oC_Expression SP? ')' ;
 */
	'(' expression=Expression ')';

RelationshipsPattern:
/*
 * oC_RelationshipsPattern : oC_NodePattern ( SP? oC_PatternElementChain )+ ;
 */
	nodePattern=NodePattern (chain+=PatternElementChain)+;

FilterExpression:
/*
 * oC_FilterExpression : oC_IdInColl ( SP? oC_Where )? ;
 */
	idInColl=IdInColl (where=Where)?;

IdInColl:
/*
 * oC_IdInColl : oC_Variable SP IN SP oC_Expression ;
 */
	variable=VariableDeclaration 'IN' expression=Expression;

FunctionInvocation:
/*
 * oC_FunctionInvocation : oC_FunctionName SP? '(' SP? ( DISTINCT SP? )? ( oC_Expression SP? ( ',' SP? oC_Expression SP? )* )? ')' ;
 */
	functionName=FunctionName '(' distinct?='DISTINCT'? (parameter+=Expression (',' parameter+=Expression)*)? ')';

FunctionName:
/*
 * oC_FunctionName : oC_SymbolicName
 *                 | EXISTS
 *                 ;
 */
	SYMBOLIC_NAME_X;

ExplicitProcedureInvocation:
/*
 * oC_ExplicitProcedureInvocation : oC_ProcedureName SP? '(' SP? ( oC_Expression SP? ( ',' SP? oC_Expression SP? )* )? ')' ;
 */
	procedureName=ProcedureName '(' (parameter+=Expression (',' parameter+=Expression)*)? ')';

ImplicitProcedureInvocation:
/*
 * oC_ImplicitProcedureInvocation : oC_ProcedureName ;
 */
	{ImplicitProcedureInvocation} procedureName=ProcedureName;

ProcedureResultField:
/*
 * oC_ProcedureResultField : oC_SymbolicName ;
 */
	SYMBOLIC_NAME_X;

ProcedureName:
/*
 * oC_ProcedureName : oC_Namespace oC_SymbolicName ;
 */
	namespace=Namespace name=SYMBOLIC_NAME_X;

Namespace:
/**
 * oC_Namespace : ( oC_SymbolicName '.' )* ;
 */
	(SYMBOLIC_NAME_X '.')*;

ListComprehension:
/*
 * oC_ListComprehension : '[' SP? oC_FilterExpression ( SP? '|' SP? oC_Expression )? SP? ']' ;
 */
	'[' filterExpression=FilterExpression ('|' expression=Expression)? ']';

PatternComprehension:
/*
 * oC_PatternComprehension : '[' SP? ( oC_Variable SP? '=' SP? )? oC_RelationshipsPattern SP? ( WHERE SP? oC_Expression SP? )? '|' SP? oC_Expression SP? ']' ;
 */
	'[' (pathVariable=VariableDeclaration '=')? pattern=RelationshipsPattern (where=Where)? '|' expression=Expression
	']';

PropertyLookup:
/*
 * oC_PropertyLookup : '.' SP? ( oC_PropertyKeyName ) ;
 */
	'.' (propertyKeyName=PropertyKeyName propertyOperator=('?' | '!') | propertyKeyName=PropertyKeyName);

CaseExpression:
/*
 * oC_CaseExpression : ( ( CASE ( SP? oC_CaseAlternatives )+ ) | ( CASE SP? oC_Expression ( SP? oC_CaseAlternatives )+ ) ) ( SP? ELSE SP? oC_Expression )? SP? END ;
 */
	(('CASE' (caseAlternatives+=CaseAlternatives)+) | ('CASE' caseExpression=Expression
	(caseAlternatives+=CaseAlternatives)+)) ('ELSE' elseExpression=Expression)? 'END';

CaseAlternatives:
/*
 * oC_CaseAlternatives : WHEN SP? oC_Expression SP? THEN SP? oC_Expression ;
 */
	'WHEN' when=Expression 'THEN' then=Expression;

VariableDeclaration returns VariableDeclaration:
/*
 * oC_Variable : oC_SymbolicName ;
 */
	{VariableDeclaration} name=SYMBOLIC_NAME_X;

StringLiteral:
/*
 * StringLiteral : ( '"' ( StringLiteral_0 | EscapedChar )* '"' )
 *               | ( '\'' ( StringLiteral_1 | EscapedChar )* '\'' )
 *               ;
 */
	value=STRING_LITERAL;

NumberLiteral:
/*
 * oC_NumberLiteral : oC_DoubleLiteral
 *                  | oC_IntegerLiteral
 *                  ;
 */
	value=Number;

MapLiteral:
/*
 * oC_MapLiteral : '{' SP? ( oC_PropertyKeyName SP? ':' SP? oC_Expression SP? ( ',' SP? oC_PropertyKeyName SP? ':' SP? oC_Expression SP? )* )? '}' ;
 * 
 * Comment: For simplicity we split up the antlr clause in 'MapLiteral' and 'MapLiteralEntry'
 */
	{MapLiteral} '{' (entries+=MapLiteralEntry (',' entries+=MapLiteralEntry)*)? '}';

MapLiteralEntry:
	key=PropertyKeyName ':' value=Expression;

LegacyParameter:
/*
 * oC_LegacyParameter : '{' SP? ( oC_SymbolicName | DecimalInteger ) SP? '}' ;
 * 
 * Comment: Format of DecimalInteger is checked via 
 * org.slizaa.neo4j.opencypher.validation.OpenCypherValidator.checkLegacyParameterFormat(LegacyParameter)
 */
	'{' parameter=(SYMBOLIC_NAME_X | Number) '}';

Parameter:
/*
 * oC_Parameter : '$' ( oC_SymbolicName | DecimalInteger ) ;
 * 
 * Comment: Format of DecimalInteger is checked via 
 * org.slizaa.neo4j.opencypher.validation.OpenCypherValidator.checkParameterFormat(Parameter)
 */
	'$' parameter=(SYMBOLIC_NAME_X | Number);

PropertyExpression:
/*
 * oC_PropertyExpression : oC_Atom ( SP? oC_PropertyLookup )+ ;
 */
	Atom (propertyLookups+=PropertyLookup)+;

PropertyKeyName:
/*
 * oC_PropertyKeyName : oC_SchemaName ;
 */
	SYMBOLIC_NAME_X;

/**
  {StringConstant} value=STRING_LITERAL | 
  * {BoolConstant}
	value=('TRUE' | 'FALSE') | {NullConstant} 'NULL' |
	* MapLiteral |
	*  
	*/
LeftArrowHead:
/*
 * Comment: reduced character set
 */
	'<';

RightArrowHead:
/*
 * Comment: reduced character set
 */
	'>';

Dash:
/*
 * Comment: reduced character set
 */
	'-';

DecimalInteger:
/*
 * Comment: Format of DecimalInteger is checked via 
 * org.slizaa.neo4j.opencypher.validation.OpenCypherValidator.checkDecimalIntegerFormat(DecimalInteger)
 */
	{DecimalInteger} value=Number;

Number hidden():
	HEX | (INT | DECIMAL) ('.' (INT | DECIMAL))?;

SYMBOLIC_NAME_X:
/*
 * symbolicNameString : UnescapedSymbolicNameString
 *                  | EscapedSymbolicNameString
 *                  | CYPHER
 *                  | EXPLAIN
 *                  | PROFILE
 *                  | USING
 *                  | PERIODIC
 *                  | COMMIT
 *                  | UNION
 *                  | ALL
 *                  | CREATE
 *                  | INDEX
 *                  | ON
 *                  | DROP
 *                  | CONSTRAINT
 *                  | ASSERT
 *                  | IS
 *                  | UNIQUE
 *                  | EXISTS
 *                  | LOAD
 *                  | CSV
 *                  | WITH
 *                  | HEADERS
 *                  | FROM
 *                  | AS
 *                  | FIELDTERMINATOR
 *                  | OPTIONAL
 *                  | MATCH
 *                  | UNWIND
 *                  | MERGE
 *                  | SET
 *                  | DELETE
 *                  | DETACH
 *                  | REMOVE
 *                  | FOREACH
 *                  | IN
 *                  | DISTINCT
 *                  | RETURN
 *                  | ORDER
 *                  | BY
 *                  | L_SKIP
 *                  | LIMIT
 *                  | DESCENDING
 *                  | DESC
 *                  | ASCENDING
 *                  | ASC
 *                  | JOIN
 *                  | SCAN
 *                  | START
 *                  | NODE
 *                  | RELATIONSHIP
 *                  | REL
 *                  | WHERE
 *                  | SHORTESTPATH
 *                  | ALLSHORTESTPATHS
 *                  | OR
 *                  | XOR
 *                  | AND
 *                  | NOT
 *                  | STARTS
 *                  | ENDS
 *                  | CONTAINS
 *                  | NULL
 *                  | TRUE
 *                  | FALSE
 *                  | COUNT
 *                  | FILTER
 *                  | EXTRACT
 *                  | ANY
 *                  | NONE
 *                  | SINGLE
 *                  | REDUCE
 *                  | CASE
 *                  | ELSE
 *                  | END
 *                  | WHEN
 *                  | THEN
 *                  | L_0X
 *                   ;
 */
	ID | 'CYPHER' | 'EXPLAIN' | 'PROFILE' | 'USING' | 'PERIODIC' | 'COMMIT' | 'UNION' | 'ALL' | 'CREATE' | 'INDEX' | 'ON'
	| 'DROP' | 'CONSTRAINT' | 'ASSERT' | 'IS' | 'UNIQUE' | 'EXISTS' | 'LOAD' | 'CSV' | 'WITH' | 'HEADERS' | 'FROM' | 'AS'
	| 'FIELDTERMINATOR' | 'OPTIONAL' | 'MATCH' | 'UNWIND' | 'MERGE' | 'SET' | 'DELETE' | 'DETACH' | 'REMOVE' | 'FOREACH'
	| 'IN' | 'DISTINCT' | 'RETURN' | 'ORDER' | 'BY' | 'SKIP' | 'LIMIT' | 'DESCENDING' | 'DESC' | 'ASCENDING' | 'ASC' |
	'JOIN' | 'SCAN' | 'START' | 'NODE' | 'RELATIONSHIP' | 'REL' | 'WHERE' | 'SHORTESTPATH' | 'ALLSHORTESTPATHS' | 'OR' |
	'XOR' | 'AND' | 'NOT' | 'STARTS' | 'ENDS' | 'CONTAINS' | 'NULL' | 'TRUE' | 'FALSE' | 'COUNT' | 'FILTER' | 'EXTRACT' |
	'ANY' | 'NONE' | 'SINGLE' | 'REDUCE' | 'CASE' | 'ELSE' | 'END' | 'WHEN' | 'THEN';

terminal ID:
/*
 * Comment: reduced character set
 */
	('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')* | ('`'->'`');

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

terminal STRING_LITERAL:
	'"' ('\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | '"'))* '"' | "'" ('\\' .
	/* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | "'"))* "'";

terminal HEX:
	('0x' | '0X') ('0'..'9' | 'A'..'F')+;

terminal INT returns ecore::EInt:
	'0'..'9' ('0'..'9')*;

terminal DECIMAL:
	INT (('e') ('+' | '-')? INT)?;
