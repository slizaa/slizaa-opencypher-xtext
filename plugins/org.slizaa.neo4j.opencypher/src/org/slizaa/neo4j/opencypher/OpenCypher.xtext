grammar org.slizaa.neo4j.opencypher.OpenCypher hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate openCypher "http://www.slizaa.org/neo4j/opencypher/OpenCypher"

Cypher:
/*
 * cypher : ws queryOptions statement ( ws ';' )? ws ;
 */
	{Cypher} queryOptions=QueryOptions statement=Statement ';'?;

QueryOptions:
/*
 * queryOptions : ( anyCypherOption ws )* ;
 * anyCypherOption : cypherOption
 *                 | explain
 *                 | profile
 *                 ;
 * explain : EXPLAIN ;
 * profile : PROFILE ;
 */
	{AllOptions} (explain?='EXPLAIN'? & profile?='PROFILE'? & cypherOption+=CypherOption*);

CypherOption:
/*
 * cypherOption : CYPHER ( sp versionNumber )? ( sp configurationOption )* ;
 */
	{CypherOption} 'CYPHER' (versionNumber=VersionNumber)? (configurationOption+=ConfigurationOption)*;

VersionNumber:
/*
 * versionNumber : DecimalInteger '.' DecimalInteger ;
 */
	versionNumber=Number;

ConfigurationOption:
/*
 * configurationOption : symbolicName ws '=' ws symbolicName ;
 */
	key=SYMBOLIC_NAME_X '=' value=SYMBOLIC_NAME_X;

Statement:
/*
 * statement : command
 *           | query;
 */
	Command | Query;

Query:
/*
 * query : regularQuery
 *       | standaloneCall
 *       | bulkImportQuery;
 */
	RegularQuery | StandaloneCall | BulkImportQuery;

RegularQuery:
/*
 * regularQuery : singleQuery ( ws union )* ;
 */
	SingleQuery ({CombinedQuery.singleQuery=current} ( union+=Union )+ )? ;

BulkImportQuery:
/*
 * bulkImportQuery : periodicCommitHint ws loadCSVQuery ;
 */
	periodicCommitHint=PeriodicCommitHint loadCSVQuery=LoadCSVQuery;

SingleQuery:
/*
 * singleQuery : clause ( ws clause )* ;
 */
	clauses+=Clause (clauses+=Clause)*;

PeriodicCommitHint:
/*
 * periodicCommitHint : USING sp PERIODIC sp COMMIT ( sp signedIntegerLiteral )? ;
 */
	{PeriodicCommitHint} 'USING' 'PERIODIC' 'COMMIT' numberOfRowsPerCommit=DecimalInteger?;

LoadCSVQuery:
/*
 * loadCSVQuery : loadCSV ( ws clause )* ;
 */
	loadCSV=LoadCSV (clauses+=Clause)*;

Union:
/*
 * union : ( UNION sp ALL singleQuery )
 *  | ( UNION singleQuery );
 */
	('UNION' all?='ALL'? singleQuery=SingleQuery);

Clause:
/*
 * clause : loadCSV
 *        | start
 *        | match
 *        | unwind
 *        | merge
 *        | create
 *        | createUnique
 *        | set
 *        | delete
 *        | remove
 *        | foreach
 *        | with
 *        | return
 *        ;
 * 
 * Comment: We handle 'createUnique' within the 'create' clause
 */
	LoadCSV | Start | Match | Unwind | Merge | Create | Set | Delete | Remove | Foreach | With | Return | InQueryCall;

Command:
/*
 * command : createIndex
 *         | dropIndex
 *         | createUniqueConstraint
 *         | dropUniqueConstraint
 *         | createNodePropertyExistenceConstraint
 *         | dropNodePropertyExistenceConstraint
 *         | createRelationshipPropertyExistenceConstraint
 *         | dropRelationshipPropertyExistenceConstraint
 *         ;
 */
	CreateIndex | DropIndex | CreateUniqueConstraint | DropUniqueConstraint | CreateNodePropertyExistenceConstraint |
	DropNodePropertyExistenceConstraint | CreateRelationshipPropertyExistenceConstraint |
	DropRelationshipPropertyExistenceConstraint;

CreateUniqueConstraint:
/*
 * createUniqueConstraint : CREATE sp uniqueConstraint ;
 */
	'CREATE' UniqueConstraint;

CreateNodePropertyExistenceConstraint:
/*
 * createNodePropertyExistenceConstraint : CREATE sp nodePropertyExistenceConstraint ;
 */
	'CREATE' NodePropertyExistenceConstraint;

CreateRelationshipPropertyExistenceConstraint:
/*
 * createRelationshipPropertyExistenceConstraint : CREATE sp relationshipPropertyExistenceConstraint ;
 */
	'CREATE' RelationshipPropertyExistenceConstraint;

CreateIndex:
/*
 * createIndex : CREATE sp index ;
 */
	'CREATE' index=Index;

DropUniqueConstraint:
/*
 * dropUniqueConstraint : DROP sp uniqueConstraint ;
 */
	'DROP' uniqueConstraint=UniqueConstraint;

DropNodePropertyExistenceConstraint:
/*
 * dropNodePropertyExistenceConstraint : DROP sp nodePropertyExistenceConstraint ;
 */
	'DROP' nodePropertyExistenceConstraint=NodePropertyExistenceConstraint;

DropRelationshipPropertyExistenceConstraint:
/*
 * dropRelationshipPropertyExistenceConstraint : DROP sp relationshipPropertyExistenceConstraint ;
 */
	'DROP' relationshipPropertyExistenceConstraint=RelationshipPropertyExistenceConstraint;

DropIndex:
/*
 * dropIndex : DROP sp index ;
 */
	'DROP' index=Index;

Index:
/*
 * index : INDEX sp ON ws nodeLabel '(' propertyKeyName ')' ;
 */
	'INDEX' 'ON' nodeLabel=NodeLabel '(' propertyKeyName=PropertyKeyName ')';

UniqueConstraint:
/*
 * uniqueConstraint : CONSTRAINT sp ON ws '(' variable nodeLabel ')' ws ASSERT sp propertyExpression sp IS sp UNIQUE ;
 */
	'CONSTRAINT' 'ON' '(' variable=VariableDeclaration nodeLabel=NodeLabel ')' 'ASSERT' propertyExpression=PropertyExpression 'IS'
	'UNIQUE';

NodePropertyExistenceConstraint:
/*
 * nodePropertyExistenceConstraint : CONSTRAINT sp ON ws '(' variable nodeLabel ')' ws ASSERT sp EXISTS ws '(' propertyExpression ')' ;
 */
	'CONSTRAINT' 'ON' '(' variable=VariableDeclaration nodeLabel=NodeLabel ')' 'ASSERT' 'EXISTS' '('
	propertyExpression=PropertyExpression ')';

RelationshipPropertyExistenceConstraint:
/*
 * relationshipPropertyExistenceConstraint : CONSTRAINT sp ON ws relationshipPatternSyntax ws ASSERT sp EXISTS ws '(' propertyExpression ')' ;
 */
	'CONSTRAINT' 'ON' relationshipPattern=RelationshipPatternSyntax 'ASSERT' 'EXISTS' '('
	propertyExpression=PropertyExpression ')';

RelationshipPatternSyntax:
/*
 * relationshipPatternSyntax : ( '(' ws ')' dash '[' variable relType ']' dash '(' ws ')' )
 *                           | ( '(' ws ')' dash '[' variable relType ']' rightArrowHead dash '(' ws ')' )
 *                           | ( '(' ws ')' leftArrowHead dash '[' variable relType ']' dash '(' ws ')' );
 */
	('(' ')' incoming?=LeftArrowHead? Dash '[' variable=VariableDeclaration relType=RelType ']' Dash outgoing?=RightArrowHead? '('
	')');

LoadCSV:
/*
 * loadCSV : LOAD sp CSV sp ( WITH sp HEADERS sp )? FROM sp expression sp AS sp variable sp ( FIELDTERMINATOR sp StringLiteral )? ;
 * 
 * Comment: For simplicity we split up the antlr clause in xtext clauses 'LoadCSV' and 'LoadCSVWithHeaders'
 */
	{LoadCSV} 'LOAD' 'CSV' withHeaders?=LoadCSVWithHeaders? 'FROM' expression=Expression 'AS' variable=VariableDeclaration
	('FIELDTERMINATOR' fieldterminator=STRING_LITERAL)?;

LoadCSVWithHeaders:
	'WITH' 'HEADERS';

Match:
/*
 * match : ( OPTIONAL sp )? MATCH ws pattern ( hint )* ( ws where )? ;
 */
	optional?='OPTIONAL'? 'MATCH' pattern=Pattern (hints+=Hint)* where=Where?;

Unwind:
/*
 * unwind : UNWIND ws expression sp AS sp variable ;
 */
	'UNWIND' expression=Expression 'AS' variable=VariableDeclaration;

Merge:
/*
 * merge : MERGE ws patternPart ( sp mergeAction )* ;
 */
	'MERGE' patternPart=PatternPart (mergeActions+=MergeAction)*;

MergeAction:
/*
 * mergeAction : ( ON sp MATCH sp set )
 *             | ( ON sp CREATE sp set )
 *             ;
 */
	'ON' action=('MATCH' | 'CREATE') set=Set;

Create:
/*
 * create : CREATE ws pattern ;
 * createUnique : CREATE sp UNIQUE ws pattern ;
 * 
 * Comment: We handle 'create' and 'createUnqiue' in the same clause
 */
	'CREATE' unique?='UNIQUE'? pattern=Pattern;

Set:
/*
 * set : SET setItem ( ',' setItem )* ;
 */
	'SET' setItems+=SetItem (',' setItems+=SetItem)*;

SetItem:
/*
 * setItem : ( propertyExpression '=' expression )
 *         | ( variable '=' expression )
 *         | ( variable '+=' expression )
 *         | ( variable nodeLabels );
 */
	(propertyExpression=PropertyExpression '=' expression=Expression) | (variable=VariableDeclaration '=' expression=Expression) |
	(variable=VariableDeclaration '+=' expression=Expression) | (variable=VariableDeclaration nodeLabels=NodeLabels);

Delete:
/*
 * delete : ( DELETE expression ( ',' expression )* )
 *        | ( DETACH sp DELETE expression ( ',' expression )* );
 */
	(detach?='DETACH')? 'DELETE' expressions+=Expression (',' expressions+=Expression)*;

Remove:
/*
 * remove : REMOVE sp removeItem ( ws ',' ws removeItem )* ;
 */
	'REMOVE' removeItems+=RemoveItem (',' removeItems+=RemoveItem)*;

RemoveItem:
/*
 * removeItem : ( variable nodeLabels )
 *              | propertyExpression;
 */
	({RemoveItemLabel} variable=VariableDeclaration nodeLabels=NodeLabels) | {RemoveItemProperty} propertyExpression=PropertyExpression;

Foreach:
/*
 * foreach : FOREACH ws '(' ws variable sp IN sp expression ws '|' ( sp clause )+ ws ')' ;
 */
	'FOREACH' '(' variable=VariableDeclaration 'IN' expression=Expression '|' (clauses+=Clause)+ ')';

InQueryCall:
/*
 * inQueryCall : CALL sp explicitProcedureInvocation ( YIELD sp yieldItems )? ;
 */
	'CALL' invocation=ExplicitProcedureInvocation ('YIELD' yieldItems=YieldItems)?;

StandaloneCall:
/*
 * standaloneCall: 'CALL' sp ( ExplicitProcedureInvocation | ImplicitProcedureInvocation ) ( sp YIELD sp YieldItems )? ;
 */
	'CALL' invocation=(ExplicitProcedureInvocation | ImplicitProcedureInvocation) ('YIELD' yieldItems=YieldItems)?;

YieldItems:
/*
 * YieldItems : ( YieldItem ( ',' YieldItem )* )
 *            | '-'
 *            ;
 */
	{YieldItems} ((items+=YieldItem (',' items+=YieldItem)*) | '-');

YieldItem:
/*
 * YieldItem : ( ProcedureResultField sp AS sp )? Variable ;
 */
	 (field=ProcedureResultField 'AS')? variable=VariableDeclaration;


With:
/*
 * with : ( WITH DISTINCT sp returnBody where? )
 *      | ( WITH sp returnBody where? );
 */
	'WITH' distint?='DISTINCT'? returnBody=ReturnBody where=Where?;

Return:
/*
 * return : ( RETURN sp DISTINCT sp returnBody )
 *        | ( RETURN sp returnBody );
 */
	(return='RETURN' distinct?='DISTINCT'? body=ReturnBody);

ReturnBody:
/*
 * returnBody : returnItems ( sp order )? ( sp skip )? ( sp limit )? ;
 */
	returnItems=ReturnItems (order=Order)? (skip=Skip)? (limit=Limit)?;

ReturnItems:
/*
 * returnItems : ( '*' ( ws ',' ws returnItem )* )
 *             | ( returnItem ( ws ',' ws returnItem )* );
 */
	{ReturnItems} (all='*' (',' items+=ReturnItem)*) | (items+=ReturnItem (',' items+=ReturnItem)*);

ReturnItem:
/*
 * returnItem : ( expression sp AS sp variable )
 *              | expression;
 */
	expression=Expression ('AS' alias=VariableDeclaration)?;

Order:
/*
 * order : ORDER sp BY sp sortItem ( ',' ws sortItem )* ;
 */
	'ORDER' 'BY' orderBy+=SortItem (',' orderBy+=SortItem)*;

Skip:
/*
 * skip: L_SKIP sp expression;
 */
	'SKIP' skip=Expression;

Limit:
/*
 * limit: LIMIT sp expression;
 */
	'LIMIT' limit=Expression;

SortItem:
/*
 * sortItem : ( expression ( DESCENDING | DESC ) )
 *          | ( expression ( ASCENDING | ASC )? );
 */
	(expression=Expression sort=('DESCENDING' | 'DESC' | 'ASCENDING' | 'ASC')?);

Hint hidden():
/*
 * hint : ws ( ( USING sp INDEX sp variable nodeLabel '(' propertyKeyName ')' ) 
 *      | ( USING sp JOIN sp ON sp variable ( ws ',' ws variable )* ) 
 *      | ( USING sp SCAN sp variable nodeLabel ) ) ;
 */
	{IndexHint} 'USING' WS 'INDEX' WS variable=VariableDeclaration nodeLabel=NodeLabel WS '(' propertyKeyName=PropertyKeyName ')' |
	{JoinHint} ('USING' WS 'JOIN' WS 'ON' variables+=VariableDeclaration (',' variables+=VariableDeclaration)*) | {ScanHint} ('USING' WS 'SCAN'
	WS variable=VariableDeclaration nodeLabel=NodeLabel);

Start:
/*
 * start : START sp startPoint ( ws ',' ws startPoint )* where? ;
 */
	'START' startPoint+=StartPoint (',' startPoint+=StartPoint)* where=Where?;

StartPoint:
/*
 * startPoint : variable ws '=' ws lookup ;
 */
	variable=VariableDeclaration '=' lookup=Lookup;

Lookup:
/*
 * lookup : nodeLookup
 *        | relationshipLookup;
 */
	NodeLookup | RelationshipLookup;

NodeLookup:
/*
 * nodeLookup : NODE ( identifiedIndexLookup | indexQuery | idLookup ) ;
 */
	'NODE' (IdentifiedIndexLookup | IndexQuery | IdLookup);

RelationshipLookup:
/*
 * relationshipLookup : ( RELATIONSHIP | REL ) ( identifiedIndexLookup | indexQuery | idLookup ) ;
 */
	('RELATIONSHIP' | 'REL') (IdentifiedIndexLookup | IndexQuery | IdLookup);

IdentifiedIndexLookup:
/*
 * identifiedIndexLookup : ':' symbolicName '(' symbolicName '=' ( StringLiteral | legacyParameter ) ')' ;
 */
	':' indexName=SYMBOLIC_NAME_X '(' key=SYMBOLIC_NAME_X '=' (value=STRING_LITERAL | legacyParameter=LegacyParameter) ')';

IndexQuery:
/*
 * indexQuery : ':' symbolicName '(' ( StringLiteral | legacyParameter ) ')' ;
 */
	':' indexName=SYMBOLIC_NAME_X '(' (query=STRING_LITERAL | parameter=Parameter) ')';

IdLookup:
/*
 * idLookup : '(' ( literalIds | legacyParameter | '*' ) ')' ;
 */
	'(' (ids=LiteralIds | legacyParameter=Parameter | wildcard='*') ')';

LiteralIds:
/*
 * literalIds : unsignedIntegerLiteral ( ws ',' ws unsignedIntegerLiteral )*;
 */
	ids+=DecimalInteger (',' ids+=DecimalInteger)*;

Where:
/*
 * where : WHERE sp expression;
 */
	'WHERE' expression=Expression;

Pattern:
/*
 * pattern : patternPart ( ',' patternPart )*;
 */
	patterns+=PatternPart (',' patterns+=PatternPart)*;

PatternPart:
/*
 * patternPart : ( variable ws '=' ws anonymousPatternPart )
 *               | anonymousPatternPart;
 */
	(var=VariableDeclaration '=' part=AnonymousPatternPart) | AnonymousPatternPart;

AnonymousPatternPart:
/*
 * anonymousPatternPart : shortestPathPattern
 *                      | patternElement;
 */
	ShortestPathPattern | PatternElement;

ShortestPathPattern:
/*
 * shortestPathPattern : ( SHORTESTPATH '(' patternElement ')' )
 *                     | ( ALLSHORTESTPATHS '(' patternElement ')' );
 */
	{ShortestPath} 'SHORTESTPATH' '(' patternElement=PatternElement ')' | {AllShortestPaths} 'ALLSHORTESTPATHS' '('
	patternElement=PatternElement ')';

PatternElement:
/*
 * patternElement : ( nodePattern ( ws patternElementChain )* )
 *                | ( '(' patternElement ')' );
 */
	(nodepattern=NodePattern (chain+=PatternElementChain)*) | '(' PatternElement ')';

NodePattern:
/*
 * nodePattern : '(' ws ( variable ws )? ( nodeLabels ws )? ( properties ws )? ')' ;
 */
	{NodePattern} '(' variable=VariableDeclaration? nodeLabels=NodeLabels? properties=Properties? ')';

PatternElementChain:
/*
 * patternElementChain : relationshipPattern ws nodePattern ;
 */
	relationshipPattern=RelationshipPattern nodePattern=NodePattern;

RelationshipPattern:
/*
 * relationshipPattern : ( leftArrowHead ws dash ws relationshipDetail? ws dash ws rightArrowHead )
 *                     | ( leftArrowHead ws dash ws relationshipDetail? ws dash )
 *                     | ( dash ws relationshipDetail? ws dash ws rightArrowHead )
 *                     | ( dash ws relationshipDetail? ws dash )
 *                     ;
 */
	{RelationshipPattern} (incoming?=LeftArrowHead? Dash detail=RelationshipDetail? Dash outgoing?=RightArrowHead?);

RelationshipDetail:
/*
 * relationshipDetail : '[' variable? '?'? relationshipTypes? ( '*' rangeLiteral )? properties? ']' ;
 */
	{RelationshipDetail} '[' variable=VariableDeclaration? optional?='?'? types=RelationshipTypes? (range=RangeLiteral)?
	properties=Properties? ']';

Properties:
/*
 * properties : mapLiteral
 *            | parameter
 *            | legacyParameter
 *            ;
 */
	MapLiteral | Parameter | LegacyParameter;

RelType:
/*
 * relType : ':' relTypeName ;
 */
	':' RelTypeName;

RelationshipTypes:
/*
 * relationshipTypes : ':' relTypeName ( ws '|' ':'? ws relTypeName )* ;
 */
	':' relTypeName+=RelTypeName ('|' ':'? relTypeName+=RelTypeName)*;

NodeLabels:
/*
 * nodeLabels : nodeLabel ( ws nodeLabel )* ;
 */
	nodeLabels+=NodeLabel (nodeLabels+=NodeLabel)*;

NodeLabel:
/*
 * nodeLabel : ':' labelName ;
 */
	':' labelName=LabelName;

RangeLiteral:
/*
 * rangeLiteral : ws ( integerLiteral ws )? ( '..' ws ( integerLiteral ws )? )? ;
 * 
 * Comment: Format of IntegerLiteral is checked via 
 * org.slizaa.neo4j.opencypher.validation.OpenCypherValidator.checkRangeLiteralFormat(RangeLiteral)
 */
	{RangeLiteral} '*' (lower=Number? ('..' upper=Number? )? )?;

LabelName:
/*
 * labelName : symbolicName ;
 */
	SYMBOLIC_NAME_X;

RelTypeName:
/*
 * relTypeName : symbolicName ;
 */
	SYMBOLIC_NAME_X;

Expression:
/*
 * expression : expression12 ;
 */
	ExpressionOr;

ExpressionOr returns Expression:
/*
 * expression12 : expression11 ( sp OR sp expression11 )* ;
 */
	ExpressionXor ({ExpressionOr.left=current} operator='OR' right=ExpressionXor)*;

ExpressionXor returns Expression:
/*
 * expression11 : expression10 ( sp XOR sp expression10 )* ;
 */
	ExpressionAnd ({ExpressionXor.left=current} operator='XOR' right=ExpressionAnd)*;

ExpressionAnd returns Expression:
/*
 * expression10 : expression9 ( sp AND sp expression9 )* ;
 */
	ExpressionNot ({ExpressionAnd.left=current} operator='AND' right=ExpressionNot)*;

ExpressionNot returns Expression:
/*
 * expression9 : ( sp NOT sp )* expression8 ;
 * 
 * Comment: Clause is modeled on the former antlr clause 'expression9 : ( sp NOT sp expression9 ) | expression8;'
 * We should eventually adjust this...
 */
	({ExpressionNot} operator='NOT' left=ExpressionNot) | ExpressionComparison;

ExpressionComparison returns Expression:
/*
 * expression8 : expression7 ( ws partialComparisonExpression )* ;
 * 
 * partialComparisonExpression : ( '=' ws expression7 )
 *                             | ( '<>' ws expression7 )
 *                             | ( '<' ws expression7 )
 *                             | ( '>' ws expression7 )
 *                             | ( '<=' ws expression7 )
 *                             | ( '>=' ws expression7 );
 * 
 * Comment: combined clause for 'expression8' and 'partialComparisonExpression'
 */
	ExpressionPlusMinus ({ExpressionComparison.left=current} operator=('=' | '<>' | '<' | '>' | '<=' | '>=')
	right=ExpressionPlusMinus)*;

ExpressionPlusMinus returns Expression:
/*
 * expression7 : expression6 ( ( ws '+' ws expression6 ) | ( ws '-' ws expression6 ) )* ;
 */
	ExpressionMulDiv ({ExpressionPlusMinus.left=current} operator=('+' | '-') right=ExpressionMulDiv)*;

ExpressionMulDiv returns Expression:
/*
 * expression6 : expression5 ( ( ws '*' ws expression5 ) | ( ws '/' ws expression5 ) | ( ws '%' ws expression5 ) )* ;
 */
	ExpressionPower ({ExpressionMulDiv.left=current} operator=('*' | '/' | '%') right=ExpressionPower)*;

ExpressionPower returns Expression:
/*
 * expression5 : expression4 ( ws '^' ws expression4 )* ;
 */
	ExpressionUnaryPlusMinus ({ExpressionPower.left=current} operator='^' right=ExpressionUnaryPlusMinus)*;

ExpressionUnaryPlusMinus returns Expression:
/*
 * expression4 : ( ( '+' | '-' ) ws )* expression3 ;
 * 
 * Comment: In order to restrict our model for a single operator at a time, clause is modeled after a rewritetten form of the antlr clause 'expression4 : expression3 | ( ( '+' | '-' ) ws ) expression4;'
 */
	Expression3 | {ExpressionUnaryPlusMinus} operator=('+' | '-') left=ExpressionUnaryPlusMinus;

Expression3 returns Expression:
/*
 * expression3 : expression2 ( ( ws '[' expression ']' ) | ( ws '[' expression? '..' expression? ']' ) | ( ( ( ws '=~' ) | ( sp IN ) | ( sp STARTS sp WITH ) | ( sp ENDS sp WITH ) | ( sp CONTAINS ) ) ws expression2 ) | ( sp IS sp NULL ) | ( sp IS sp NOT sp NULL ) )* ;
 */
	ExpressionNodeLabelsAndPropertyLookup (({IndexExpression.left=current} '[' expression=Expression ('..'
	upper=Expression)? ']') | ({RegExpMatchingExpression.left=current} '=~'
	right=ExpressionNodeLabelsAndPropertyLookup) | ({InCollectionExpression.left=current} 'IN'
	right=ExpressionNodeLabelsAndPropertyLookup) | ({StartsWithExpression.left=current} 'STARTS' 'WITH'
	right=ExpressionNodeLabelsAndPropertyLookup) | ({EndsWithExpression.left=current} 'ENDS' 'WITH'
	right=ExpressionNodeLabelsAndPropertyLookup) | ({ContainsExpression.left=current} 'CONTAINS'
	right=ExpressionNodeLabelsAndPropertyLookup) | ({IsNullExpression.left=current} 'IS' 'NULL') |
	({IsNotNullExpression.left=current} 'IS' 'NOT' 'NULL'))*;

ExpressionNodeLabelsAndPropertyLookup returns Expression:
/*
 * this is a clarified and corrected form of openCypher spec M08 version which had ambiguity
 * oC_PropertyOrLabelsExpression : oC_Atom ( SP? oC_PropertyLookup )* ( SP? oC_NodeLabels )?
 *
 * Note: we want ExpressionPropertyLookup and ExpressionNodeLabelsConstraint to
 * appear in the AST iff property lookups and node label constraints were given, respectively
 */
	Atom ({ExpressionPropertyLookup.left=current} propertyLookups+=PropertyLookup+)?
	     ({ExpressionNodeLabels.left=current} nodeLabels+=NodeLabel+)?
	;

/* Expression1 */
Atom returns Expression:
/*
 * atom : numberLiteral
 *      | StringLiteral
 *      | parameter
 *      | legacyParameter
 *      | TRUE
 *      | FALSE
 *      | NULL
 *      | caseExpression
 *      | ( COUNT '(' '*' ')' )
 *      | mapLiteral
 *      | listComprehension
 *      | patternComprehension
 *      | ( '[' ws expression ws ( ',' ws expression ws )* ']' )
 *      | ( FILTER ws '(' ws filterExpression ws ')' )
 *      | ( EXTRACT ws '(' ws filterExpression ws ( ws '|' expression )? ')' )
 *      | reduce
 *      | ( ALL ws '(' ws filterExpression ws ')' )
 *      | ( ANY ws '(' ws filterExpression ws ')' )
 *      | ( NONE ws '(' ws filterExpression ws ')' )
 *      | ( SINGLE ws '(' ws filterExpression ws ')' )
 *      | shortestPathPattern
 *      | relationshipsPattern
 *      | parenthesizedExpression
 *      | functionInvocation
 *      | variable
 *      ;
 */
	{NumberConstant} value=Number |
	{StringConstant} value=STRING_LITERAL |
	Parameter |
	LegacyParameter |
	{BoolConstant} value=('TRUE' | 'FALSE') |
	{NullConstant} 'NULL' |
	CaseExpression |
	{Count} 'COUNT' '(' '*' ')' |
	MapLiteral |
	ListComprehension |
	PatternComprehension |
	{ExpressionList} ('[' ( expressions+=Expression (',' expressions+=Expression)* )? ']') |
	{Extract} 'EXTRACT' '(' filterExpression=FilterExpression ('|' expression=Expression)? ')' |
	Reduce |
	{Filter} 'FILTER' '(' filterExpression=FilterExpression ')' |
	{All} 'ALL' '(' filterExpression=FilterExpression ')' |
	{Any} 'ANY' '(' filterExpression=FilterExpression ')' |
	{None} 'NONE' '(' filterExpression=FilterExpression ')' |
	{Single} 'SINGLE' '(' filterExpression=FilterExpression ')' |
	ShortestPathPattern |
	RelationshipsPattern |
	ParenthesizedExpression |
	FunctionInvocation |
	{VariableRef} variableRef=[VariableDeclaration|SYMBOLIC_NAME_X];

Reduce:
/*
 * reduce : REDUCE ws '(' variable '=' expression ',' idInColl '|' expression ')' ;
 */
	'REDUCE' '(' accumulator=VariableDeclaration '=' accumulatorExpression=Expression ',' idInColl=IdInColl '|'
	expression=Expression ')';

ParenthesizedExpression:
/*
 * parenthesizedExpression : '(' ws expression ws ')' ;
 */
	'(' expression=Expression ')';

RelationshipsPattern:
/*
 * relationshipsPattern : nodePattern ( ws patternElementChain )+ ;
 */
	nodePattern=NodePattern (chain+=PatternElementChain)+;

FilterExpression:
/*
 * filterExpression : idInColl ( ws where )? ;
 */
	idInColl=IdInColl (where=Where)?;

IdInColl:
/*
 * idInColl : variable sp IN sp expression ;
 */
	variable=VariableDeclaration 'IN' expression=Expression;

FunctionInvocation:
/*
 * functionInvocation : functionName ws '(' ws DISTINCT? ( expression ( ',' ws expression )* )? ws ')' ;
 */
	functionName=FunctionName '(' distinct?='DISTINCT'? (parameter+=Expression (',' parameter+=Expression)*)? ')';

FunctionName:
/*
 * functionName : symbolicName ;
 */
	SYMBOLIC_NAME_X;

ExplicitProcedureInvocation:
/*
 * ExplicitProcedureInvocation : ProcedureName '(' ( Expression ( ',' Expression )* )? ')' ;
 */
	procedureName=ProcedureName '(' (parameter+=Expression (',' parameter+=Expression)*)? ')';

ProcedureResultField:
/*
 * ProcedureResultField : SymbolicName ;
 */
	SYMBOLIC_NAME_X;

ImplicitProcedureInvocation:
/*
 * ImplicitProcedureInvocation : ProcedureName ;
 */
	ProcedureName;


ProcedureName:
/*
 * ProcedureName : Namespace SymbolicName ;
 */
	 namespace=Namespace name=SYMBOLIC_NAME_X;

Namespace:
/**
 * Namespace : ( SymbolicName '.' )* ;
 */
	(SYMBOLIC_NAME_X '.')*;


ListComprehension:
/*
 * listComprehension : '[' filterExpression ( ws '|' expression )? ']' ;
 */
	'[' filterExpression=FilterExpression ('|' expression=Expression)? ']';

PatternComprehension:
/*
 * patternComprehension : '['  ( variable '=' )? relationshipsPattern ( WHERE expression )? '|' expression ']' ;
 */
 	'[' ( pathVariable=VariableDeclaration '=' )? pattern=RelationshipsPattern ( where=Where )? '|' expression=Expression ']';

PropertyLookup:
/*
 * propertyLookup : ws '.' ws ( ( propertyKeyName ( '?' | '!' ) ) | propertyKeyName ) ;
 */
	'.' (propertyKeyName=PropertyKeyName propertyOperator=('?' | '!') | propertyKeyName=PropertyKeyName);

CaseExpression:
/*
 * caseExpression : ( ( CASE ( ws caseAlternatives )+ ) | ( CASE expression ( ws caseAlternatives )+ ) ) ( ws ELSE ws expression )? ws END ;
 */
	(('CASE' (caseAlternatives+=CaseAlternatives)+) | ('CASE' caseExpression=Expression
	(caseAlternatives+=CaseAlternatives)+)) ('ELSE' elseExpression=Expression)? 'END';

CaseAlternatives:
/*
 * caseAlternatives : WHEN ws expression ws THEN ws expression ;
 */
	'WHEN' when=Expression 'THEN' then=Expression;

VariableDeclaration returns VariableDeclaration:
/*
 * variable : symbolicName ;
 */
	{VariableDeclaration} name=SYMBOLIC_NAME_X;

MapLiteral:
/*
 * mapLiteral : '{' ws ( propertyKeyName ws ':' ws expression ws ( ',' ws propertyKeyName ws ':' ws expression ws )* )? '}' ;
 * 
 * Comment: For simplicity we split up the antlr clause in 'MapLiteral' and 'MapLiteralEntry'
 */
	{MapLiteral} '{' (entries+=MapLiteralEntry (',' entries+=MapLiteralEntry)*)? '}';

MapLiteralEntry:
	key=PropertyKeyName ':' value=Expression;

LegacyParameter:
/*
 * legacyParameter : '{' ws ( symbolicName | DecimalInteger ) ws '}' ;
 * 
 * Comment: Format of DecimalInteger is checked via 
 * org.slizaa.neo4j.opencypher.validation.OpenCypherValidator.checkLegacyParameterFormat(LegacyParameter)
 */
	'{' parameter=(SYMBOLIC_NAME_X | Number) '}';

Parameter:
/*
 * parameter : '$' ( symbolicName | DecimalInteger ) ;
 * 
 * Comment: Format of DecimalInteger is checked via 
 * org.slizaa.neo4j.opencypher.validation.OpenCypherValidator.checkParameterFormat(Parameter)
 */
	'$' parameter=(SYMBOLIC_NAME_X | Number);

PropertyExpression:
/*
 * propertyExpression : atom ( ws propertyLookup )+ ;
 */
	left=Atom propertyLookups+=PropertyLookup+;

PropertyKeyName:
/*
 * propertyKeyName : symbolicName ;
 */
	SYMBOLIC_NAME_X;

LeftArrowHead:
/*
 * Comment: reduced character set
 */
	'<';

RightArrowHead:
/*
 * Comment: reduced character set
 */
	'>';

Dash:
/*
 * Comment: reduced character set
 */
	'-';

DecimalInteger:
/*
 * Comment: Format of DecimalInteger is checked via 
 * org.slizaa.neo4j.opencypher.validation.OpenCypherValidator.checkDecimalIntegerFormat(DecimalInteger)
 */
	{DecimalInteger} value=Number;

Number hidden():
	HEX | (INT | DECIMAL) ('.' (INT | DECIMAL))?;

SYMBOLIC_NAME_X:
/*
 * symbolicNameString : UnescapedSymbolicNameString
 *                  | EscapedSymbolicNameString
 *                  | CYPHER
 *                  | EXPLAIN
 *                  | PROFILE
 *                  | USING
 *                  | PERIODIC
 *                  | COMMIT
 *                  | UNION
 *                  | ALL
 *                  | CREATE
 *                  | INDEX
 *                  | ON
 *                  | DROP
 *                  | CONSTRAINT
 *                  | ASSERT
 *                  | IS
 *                  | UNIQUE
 *                  | EXISTS
 *                  | LOAD
 *                  | CSV
 *                  | WITH
 *                  | HEADERS
 *                  | FROM
 *                  | AS
 *                  | FIELDTERMINATOR
 *                  | OPTIONAL
 *                  | MATCH
 *                  | UNWIND
 *                  | MERGE
 *                  | SET
 *                  | DELETE
 *                  | DETACH
 *                  | REMOVE
 *                  | FOREACH
 *                  | IN
 *                  | DISTINCT
 *                  | RETURN
 *                  | ORDER
 *                  | BY
 *                  | L_SKIP
 *                  | LIMIT
 *                  | DESCENDING
 *                  | DESC
 *                  | ASCENDING
 *                  | ASC
 *                  | JOIN
 *                  | SCAN
 *                  | START
 *                  | NODE
 *                  | RELATIONSHIP
 *                  | REL
 *                  | WHERE
 *                  | SHORTESTPATH
 *                  | ALLSHORTESTPATHS
 *                  | OR
 *                  | XOR
 *                  | AND
 *                  | NOT
 *                  | STARTS
 *                  | ENDS
 *                  | CONTAINS
 *                  | NULL
 *                  | TRUE
 *                  | FALSE
 *                  | COUNT
 *                  | FILTER
 *                  | EXTRACT
 *                  | ANY
 *                  | NONE
 *                  | SINGLE
 *                  | REDUCE
 *                  | CASE
 *                  | ELSE
 *                  | END
 *                  | WHEN
 *                  | THEN
 *                  | L_0X
 *                   ;
 */
	ID | 'CYPHER' | 'EXPLAIN' | 'PROFILE' | 'USING' | 'PERIODIC' | 'COMMIT' | 'UNION' | 'ALL' | 'CREATE' |
	'INDEX' | 'ON' | 'DROP' | 'CONSTRAINT' | 'ASSERT' | 'IS' | 'UNIQUE' | 'EXISTS' | 'LOAD' | 'CSV' | 'WITH' | 'HEADERS'
	| 'FROM' | 'AS' | 'FIELDTERMINATOR' | 'OPTIONAL' | 'MATCH' | 'UNWIND' | 'MERGE' | 'SET' | 'DELETE' | 'DETACH' |
	'REMOVE' | 'FOREACH' | 'IN' | 'DISTINCT' | 'RETURN' | 'ORDER' | 'BY' | 'SKIP' | 'LIMIT' | 'DESCENDING' | 'DESC' |
	'ASCENDING' | 'ASC' | 'JOIN' | 'SCAN' | 'START' | 'NODE' | 'RELATIONSHIP' | 'REL' | 'WHERE' | 'SHORTESTPATH' |
	'ALLSHORTESTPATHS' | 'OR' | 'XOR' | 'AND' | 'NOT' | 'STARTS' | 'ENDS' | 'CONTAINS' | 'NULL' | 'TRUE' | 'FALSE' |
	'COUNT' | 'FILTER' | 'EXTRACT' | 'ANY' | 'NONE' | 'SINGLE' | 'REDUCE' | 'CASE' | 'ELSE' | 'END' | 'WHEN' | 'THEN';

terminal ID:
/*
 * Comment: reduced character set
 */
	('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')* | ('`' -> '`');

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT:
	'//' !('\n' | '\r')* ('\r'? '\n')?;

terminal WS:
	(' ' | '\t' | '\r' | '\n')+;

terminal STRING_LITERAL:
	'"' ('\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | '"'))* '"' | "'" ('\\' .
	/* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | "'"))* "'";

terminal HEX:
	('0x' | '0X') ('0'..'9' | 'A'..'F')+;

terminal INT returns ecore::EInt:
	'0'..'9' ('0'..'9')*;

terminal DECIMAL:
	INT (('e') ('+' | '-')? INT)?;
